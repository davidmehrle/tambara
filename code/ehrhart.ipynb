{
 "cells": [
  {
   "cell_type": "code",
   "execution_count": 40,
   "id": "664756aa",
   "metadata": {
    "vscode": {
     "languageId": "sage"
    }
   },
   "outputs": [],
   "source": [
    "load(\"burnsidelib.py\")"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 41,
   "id": "9e2e5456",
   "metadata": {
    "vscode": {
     "languageId": "sage"
    }
   },
   "outputs": [],
   "source": [
    "# Take an element X of a Burnside ring of a finite group G\n",
    "# and a nonnegative integer k. Computes the k-th Siebeneicher power of X.\n",
    "# This is (in particular) a G-set whose permutation representation is the k-th\n",
    "# exterior power of the permutation representation of X.\n",
    "def siebeneicher_operation(X,k):\n",
    "    A = X.parent()\n",
    "    nG = A._num_cc_subgroups # dimension of the table of marks\n",
    "    repsG = A._cc_reps # reps of conjugacy classes\n",
    "    marks_in = X.marks()\n",
    "    marks_out = vector(ZZ,nG)\n",
    "    R.<t> = PowerSeriesRing(QQ, default_prec=k+1)\n",
    "    for i in range(nG):\n",
    "        res_X = X.res(repsG[i])\n",
    "        matH = res_X.parent().table_of_marks()\n",
    "        mark_H_lprime_t_X = prod((1-(-t)^(matH[j][0]))^c for j,c in enumerate(res_X.coeffs()))\n",
    "        marks_out[i] = mark_H_lprime_t_X.padded_list(k+1)[k]\n",
    "    return A.from_marks(marks_out)"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 110,
   "id": "c52ee778",
   "metadata": {
    "vscode": {
     "languageId": "sage"
    }
   },
   "outputs": [
    {
     "data": {
      "text/plain": [
       "[(1, 0, -1, 0, 0), (0, 1, 1, 0, 0)]"
      ]
     },
     "execution_count": 110,
     "metadata": {},
     "output_type": "execute_result"
    }
   ],
   "source": [
    "from sage.combinat.combinat import eulerian_polynomial\n",
    "\n",
    "G = gap(\"SmallGroup(4,2)\") # Klein four group\n",
    "\n",
    "A_G = BurnsideRing(G)\n",
    "X = A_G.from_vec([1,0,0,0,0]) # P is a G-invariant polytope in the permutation representation of X\n",
    "d = X.marks()[0] # d is the dimension of the permutation representation\n",
    "\n",
    "# a[0], ..., a[d] are the coefficients of the polynomial function t \\mapsto #(tP \\cap Z^n)\n",
    "# So we work over the polynomial ring S = A(G)[a_0, ..., a_d]\n",
    "S = PolynomialRing(A_G, d+1, 'a')\n",
    "a = S.gens()\n",
    "\n",
    "R.<t> = PowerSeriesRing(S, 't') # R = A(G)[a_0, ..., a_d][[t]]\n",
    "# The eulerian polynomials defined by sage are shifted from ours\n",
    "def f(i):\n",
    "    return t*eulerian_polynomial(i+1)\n",
    "\n",
    "# Compute the Equivariant Ehrhart series of P!!\n",
    "h_star = sum(a[i]*f(i)*(1-t)^(-i) for i in range(d+1))*sum(siebeneicher_operation(X,i)*(-t)^i for i in range(d+1))\n",
    "\n",
    "# We want the coefficients after t^d to be zero in the representation ring. So, we should compute the kernel of the linearization map.\n",
    "linearization_map = matrix(ZZ, map(lambda c: c.ValuesOfClassFunction(), G.CharacterTable().PermCharsTom(A_G._tom)))\n",
    "\n",
    "# M is the matrix such that (a_0 ... a_d) * M = 0 forces the coefficients of t^(d+1), ..., t^(d_max) in h^* to be zero in the representation ring.\n",
    "d_max = 3*d\n",
    "h_star_coeffs = h_star.padded_list(d_max+1)\n",
    "blocks = [matrix(ZZ, (h_star_coeffs[i].monomial_coefficient(a[j]).coeffs() * linearization_map for j in range(d+1))) for i in range(d+1, d_max+1)]\n",
    "M = block_matrix(blocks, nrows = 1)\n",
    "\n",
    "# So we need (a_0 ... a_d) to be in the left kernel of M.\n",
    "M.left_kernel().basis()"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "f07a9738",
   "metadata": {
    "vscode": {
     "languageId": "sage"
    }
   },
   "outputs": [],
   "source": []
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "SageMath 10.8",
   "language": "sage",
   "name": "SageMath-10.8"
  },
  "language_info": {
   "codemirror_mode": {
    "name": "ipython",
    "version": 3
   },
   "file_extension": ".py",
   "mimetype": "text/x-python",
   "name": "python",
   "nbconvert_exporter": "python",
   "pygments_lexer": "ipython3",
   "version": "3.13.7"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 5
}
