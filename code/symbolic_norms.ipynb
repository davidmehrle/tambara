{
 "cells": [
  {
   "cell_type": "markdown",
   "id": "7a81470d",
   "metadata": {},
   "source": [
    "Fix a subgroup inclusion $H\\leq G$. The function ``print_symbolic_norm`` displays formulas for the coefficients for the norm\n",
    "$$ \n",
    "    \\text{nm}_H^G \\left( \\sum a_i \\cdot [H/K_i] \\right) \\in A(G)\n",
    "$$\n",
    "as polynomials in the coefficients $a_i$."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "4aa7df35",
   "metadata": {
    "vscode": {
     "languageId": "sage"
    }
   },
   "outputs": [],
   "source": [
    "def print_symbolic_norm(H,G):\n",
    "    # if H is not a subgroup of G, then throw an error \n",
    "    if not gap.IsSubgroup(G,H): \n",
    "        raise ValueError(\"You fool! H must be a subgroup of G\") # maybe the error could be nicer\n",
    "\n",
    "    tomG = G.TableOfMarks() # create the table of marks of G\n",
    "    matG = matrix(ZZ, tomG.MatTom()) # table of marks as a matrix \n",
    "    nG = matG.dimensions()[0] # dimensions of the table of marks\n",
    "    repsG = [tomG.RepresentativeTom(n+1) for n in range(nG)] # reps of conjugacy classes\n",
    "\n",
    "    tomH = H.TableOfMarks() # create the table of marks of H\n",
    "    matH = matrix(ZZ, tomH.MatTom()) # table of marks as a matrix\n",
    "    nH = matH.dimensions()[0] # dimension of the table of marks\n",
    "    repsH = [tomH.RepresentativeTom(n+1) for n in range(nH)] # reps of conjugacy classes\n",
    "    \n",
    "    R = PolynomialRing(QQ, nH, \"a\") # polynomial ring with nH many generators\n",
    "    a = vector(R, R.gens()) # vector over symbolic ring R\n",
    "\n",
    "    marksa = a * matH # convert the vector f of coefficients into an element of the ghost ring\n",
    "    nm = vector(R, [1]*nG) # empty product is 1; setup for looping\n",
    "    for i in range(nG):\n",
    "        K = repsG[i] # we're finding the component indexed by K\n",
    "        for(g,_) in G.DoubleCosetRepsAndSizes(K,H): # only need the reps of double cosets\n",
    "            HcapKg = gap.Intersection(H,K.ConjugateGroup(g)) # form the intersection of H and the conjugate of K by g\n",
    "            index = next(j for j,r in enumerate(repsH) if H.IsConjugate(r, HcapKg)) # find the index of the subgroup of H that represents H \\cap K^g\n",
    "            nm[i] *= marksa[index] # multiply the [K] component of the norm by marksf([H \\cap K^g])\n",
    "    nm = matG.solve_left(nm) # convert back into a list of coefficients\n",
    "    \n",
    "    print(\"============ Norm from\", gap.StructureDescription(H), \"to\", gap.StructureDescription(G), \"============\\n\")    \n",
    "    print(\"Norm of\\n\")\n",
    "\n",
    "    for i in range(nH):\n",
    "        if i == 0:\n",
    "            print('\\t',end='')\n",
    "        print(a[i],\" \"+str(gap.StructureDescription(H)),\"/\",str(gap.StructureDescription(repsH[i])),sep='',end='')\n",
    "        if i < nH-1:\n",
    "            print(\" + \",end='')\n",
    "    \n",
    "    print(\"\\n\\nhas coefficients:\\n\")\n",
    "        \n",
    "    for i in range(nG):\n",
    "        print(\"coefficient of \" + str(gap.StructureDescription(G)) + \"/\" + str(gap.StructureDescription(repsG[i])) + \":\",end='')\n",
    "        print(\"\\t\",factor(nm[i]))\n",
    "        # uncomment this line if you want latex output\n",
    "        # long term we should probably add a ton of optional argumnets to this\n",
    "        # show(nm[i]) "
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 156,
   "id": "465cd584",
   "metadata": {
    "scrolled": false,
    "vscode": {
     "languageId": "sage"
    }
   },
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "============ Norm from C2 to Q8 ============\n",
      "\n",
      "Norm of\n",
      "\n",
      "\ta0 C2/1 + a1 C2/C2\n",
      "\n",
      "has coefficients:\n",
      "\n",
      "coefficient of Q8/1:\t a0 * (a0 + a1) * (2*a0^2 + 2*a0*a1 + a1^2)\n",
      "coefficient of Q8/C2:\t (1/4) * a1 * (a1 + 2) * (a1 - 1)^2\n",
      "coefficient of Q8/C4:\t (1/2) * (a1 - 1) * a1\n",
      "coefficient of Q8/C4:\t (1/2) * (a1 - 1) * a1\n",
      "coefficient of Q8/C4:\t (1/2) * (a1 - 1) * a1\n",
      "coefficient of Q8/Q8:\t a1\n"
     ]
    }
   ],
   "source": [
    "Q = gap(\"QuaternionGroup(8)\")\n",
    "subgroupsQ = [H.Representative() for H in Q.ConjugacyClassesSubgroups()]\n",
    "e = subgroupsQ[0]\n",
    "C2 = subgroupsQ[1]\n",
    "I = subgroupsQ[2]\n",
    "J = subgroupsQ[3]\n",
    "K = subgroupsQ[4]\n",
    "\n",
    "print_symbolic_norm(C2,Q)\n"
   ]
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "SageMath 10.7",
   "language": "sage",
   "name": "SageMath-10.7"
  },
  "language_info": {
   "codemirror_mode": {
    "name": "ipython",
    "version": 3
   },
   "file_extension": ".py",
   "mimetype": "text/x-python",
   "name": "python",
   "nbconvert_exporter": "python",
   "pygments_lexer": "ipython3",
   "version": "3.13.3"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 5
}
