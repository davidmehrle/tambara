{
 "cells": [
  {
   "cell_type": "code",
   "execution_count": 6,
   "id": "227d084d-70d2-4ae9-9748-d5c378fffe2b",
   "metadata": {
    "vscode": {
     "languageId": "sage"
    }
   },
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "env: SAGE_MACAULAY2_COMMAND=/opt/homebrew/bin/M2\n"
     ]
    }
   ],
   "source": [
    "%env SAGE_MACAULAY2_COMMAND /opt/homebrew/bin/M2"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 43,
   "id": "17692496-d2e7-4dcd-94d9-bdecaa5c3d91",
   "metadata": {
    "vscode": {
     "languageId": "sage"
    }
   },
   "outputs": [],
   "source": [
    "# Given a group G as GAP input\n",
    "# Returns a pair (A(G), genDisc) where \n",
    "#       - A(G) is the Burnside Ring of G (as a SAGE object)\n",
    "#       - genDisc is a dictionary of structure descriptions of the generators\n",
    "#       - note that this structure description does not distinguish between distinct but abstractly isomorphic subgroups\n",
    "def burnside_ring(G):\n",
    "    tomG = G.TableOfMarks()\n",
    "    matG = Matrix(ZZ, tomG.MatTom())\n",
    "    nG = matG.dimensions()[0]\n",
    "    repsG = [tomG.RepresentativeTom(n+1).StructureDescription() for n in range(nG)]\n",
    "    baseRing = PolynomialRing(ZZ,'x',nG)\n",
    "    baseGens = baseRing.gens()\n",
    "    index_of_trivial = nG-1 # GAP always put the trivial orbit at the end of the list\n",
    "    relations = [baseGens[index_of_trivial]-1]\n",
    "    for i in range(nG):\n",
    "        for j in range(i,nG):\n",
    "            relations.append(baseGens[i]*baseGens[j] - sum(coeff * baseGens[k] for k,coeff in enumerate(matG.solve_left(vector(ZZ, [matG[i][k] * matG[j][k] for k in range(nG)])))))\n",
    "    burnsideRing = baseRing.quotient(relations)\n",
    "    generatorDescriptions = { baseGens[i] : str(repsG[index_of_trivial]) + \"/\" + str(repsG[i]) for i in range(nG) }\n",
    "    return (burnsideRing,generatorDescriptions)\n",
    "\n",
    "def print_minimal_presentation(ring):\n",
    "    print(macaulay2(ring).minimalPresentation())"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 13,
   "id": "41229ccc",
   "metadata": {
    "vscode": {
     "languageId": "sage"
    }
   },
   "outputs": [],
   "source": [
    "# A function to compute the norm from H to G in the Burnside ring\n",
    "#\n",
    "# INPUT\n",
    "# H, G are GAP groups\n",
    "# f is a vector defining a linear combination of H-orbits\n",
    "#   The order of orbits is the same as the rows of the table of marks of H, as given by GAP\n",
    "#   For example, if H is the quaternion group Q_8, then the vector f = (0,1,-1,0,2,0)\n",
    "#   represents the element Q_8/C_2 - Q_8/I + 2 Q_8/K, where I = <i> and K = <k>. \n",
    "#   \n",
    "#   For Q_8, the GAP table of marks presents conjugacy classes of subgroups in the order \n",
    "#   e, C_2, I, J, K, Q_8, where I = <i>, J = <j>, and K = <k>. \n",
    "#\n",
    "# OUTPUT \n",
    "# a vector n encoding a linear combination of G-orbits, with order of orbits as above\n",
    "#\n",
    "# EXAMPLES \n",
    "#  tbd\n",
    "#\n",
    "def norm(H,G,f):\n",
    "    # if H is not a subgroup of G, then throw an error \n",
    "    if not gap.IsSubgroup(G,H): \n",
    "        raise ValueError(\"You fool! H must be a subgroup of G\") # maybe the error could be nicer\n",
    "\n",
    "    tomG = G.TableOfMarks() # create the table of marks of G\n",
    "    nG = len(G.ConjugacyClassesSubgroups()) # dimension of table of marks\n",
    "    matG = matrix(ZZ, tomG.MatTom()) # table of marks as a matrix \n",
    "    repsG = [tomG.RepresentativeTom(n+1) for n in range(nG)] # reps of conjugacy classes\n",
    "\n",
    "    tomH = H.TableOfMarks() # create the table of marks of H\n",
    "    nH = len(H.ConjugacyClassesSubgroups()) # dimension of table of marks\n",
    "    matH = matrix(ZZ, tomH.MatTom()) # table of marks as a matrix\n",
    "    repsH = [tomH.RepresentativeTom(n+1) for n in range(nH)] # reps of conjugacy classes\n",
    "\n",
    "    marksf = f * matH # convert the vector f of coefficients into an element of the ghost ring\n",
    "    nm = vector(ZZ, [1]*nG) # empty product is 1; setup for looping\n",
    "    for i in range(nG):\n",
    "        K = repsG[i] # we're finding the component indexed by K\n",
    "        for(g,_) in G.DoubleCosetRepsAndSizes(K,H): # only need the reps of double cosets\n",
    "            HcapKg = gap.Intersection(H,K.ConjugateGroup(g)) # form the intersection of H and the conjugate of K by g\n",
    "            index = next(j for j,r in enumerate(repsH) if H.IsConjugate(r, HcapKg)) # find the index of the subgroup of H that represents H \\cap K^g\n",
    "            nm[i] *= marksf[index] # multiply the [K] component of the norm by marksf([H \\cap K^g])\n",
    "    return matG.solve_left(nm) # convert back into a list of coefficients"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 28,
   "id": "f5310a5a",
   "metadata": {
    "vscode": {
     "languageId": "sage"
    }
   },
   "outputs": [
    {
     "data": {
      "text/plain": [
       "(4341, 0, 264, 11, 14, 0, 0, 2, 0, 0, 6, 0, 3, 0, 0, 0, 0, 0, 0)"
      ]
     },
     "execution_count": 28,
     "metadata": {},
     "output_type": "execute_result"
    }
   ],
   "source": [
    "S5 = gap(\"SymmetricGroup(5)\")\n",
    "S4 = gap(\"SymmetricGroup(4)\")\n",
    "C2 = gap(\"SymmetricGroup(2)\")\n",
    "e = gap(\"SymmetricGroup(1)\")\n",
    "# norm from e to C2 of 0 is 0 and of 1 is 1\n",
    "# in general norm from e to C2 of a positive a is (a choose 2, a)\n",
    "assert norm(e,C2,vector([0])) == vector([0,0])\n",
    "assert norm(e,C2,vector([1])) == vector([0,1])\n",
    "assert norm(e,C2,vector([4])) == vector([6,4])\n",
    "\n",
    "# norm from e to S5 of 0 is 0 and of 1 is 1\n",
    "# these are slow, comment out if you don't want to wait 4s\n",
    "assert norm(e,S5,vector([0])) == vector(ZZ,[0] * 19)\n",
    "assert norm(e,S5,vector([1])) == vector(ZZ, 19, {18:1})\n",
    "\n",
    "norm(S4, S5, vector(ZZ, 11, {3:1,4:1}))"
   ]
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "SageMath 10.7",
   "language": "sage",
   "name": "SageMath-10.7"
  },
  "language_info": {
   "codemirror_mode": {
    "name": "ipython",
    "version": 3
   },
   "file_extension": ".py",
   "mimetype": "text/x-python",
   "name": "python",
   "nbconvert_exporter": "python",
   "pygments_lexer": "ipython3",
   "version": "3.13.3"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 5
}
